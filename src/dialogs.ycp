/* ------------------------------------------------------------------------------
 * Copyright (c) 2006 Novell, Inc. All Rights Reserved.
 *
 *
 * This program is free software; you can redistribute it and/or modify it under
 * the terms of version 2 of the GNU General Public License as published by the
 * Free Software Foundation.
 *
 * This program is distributed in the hope that it will be useful, but WITHOUT
 * ANY WARRANTY; without even the implied warranty of MERCHANTABILITY or FITNESS
 * FOR A PARTICULAR PURPOSE.  See the GNU General Public License for more details.
 *
 * You should have received a copy of the GNU General Public License along with
 * this program; if not, contact Novell, Inc.
 *
 * To contact Novell about this file by physical or electronic mail, you may find
 * current contact information at www.novell.com.
 * ------------------------------------------------------------------------------
 */

/**
 * File:	include/support/dialogs.ycp
 * Package:	Configuration of support
 * Summary:	Dialogs definitions
 * Authors:	Michal Zugec <mzugec@novell.com>
 *
 * $Id: dialogs.ycp 27914 2006-02-13 14:32:08Z locilka $
 */

{

textdomain "support";

import "Label";
import "Report";
import "Wizard";
import "Support";
import "Map";
include "support/helps.ycp";
include "support/supportconfig_conf.ycp";

any OverviewDialog(){

    /* Command line parameters dialog caption */
    string caption = _("Supportconfig Overview Dialog");

    /* Support configure1 dialog contents */
    term contents = `HBox (`HStretch (), `VBox(
       `Frame(_("Open Novell Support Center"),
           `VBox(
               `HStretch (),
               `Left (`Label(_("This will start a browser with Novell Support Center Portal."))),
               `HBox (
                   `HStretch(),
                   `HStretch(),
                   `HStretch(),
                   `HStretch(),
                   `MinWidth (25, `PushButton(`id(`browser), _("Open"))),
                   `HStretch(),
                   `HStretch(),
                   `HStretch(),
                   `HStretch()
               )
           )
       ),
	`VSpacing(2),
       `Frame(`opt (`hstretch), _("Collect Data"),
           `VBox(
               `HStretch (),
               `Left (`Label(_("This will create tarball with collected log files."))),
               `HBox (
                   `HStretch(),
                   `HStretch(),
                   `HStretch(),
                   `HStretch(),
                   `MinWidth (25, `PushButton(`id(`tarball), _("Create report tarball"))),
                   `HStretch(),
                   `HStretch(),
                   `HStretch(),
                   `HStretch()
               )
           )
       ),

	`VSpacing(2),
       `Frame(`opt (`hstretch), _("Upload Data"),
           `VBox(
               `HStretch (),
               `Label(_("This will upload collected logs into specified URL.")),
               `HBox (
                   `HStretch(),
                   `HStretch(),
                   `HStretch(),
                   `HStretch(),
                   `MinWidth (25, `PushButton(`id(`upload), _("Upload"))),
                   `HStretch(),
                   `HStretch(),
                   `HStretch(),
                   `HStretch()

		)
           )
       )
    ), `HStretch());

    Wizard::SetContentsButtons(caption, contents, HELPS["overview"]:"",
	    Label::BackButton(), Label::FinishButton());

    Wizard::DisableBackButton();

    any ret = nil;
    while(true) {
	ret = UI::UserInput();
	if (ret == `abort || ret == `cancel || ret == `tarball || ret == `upload || ret == `next)
		break;
         else
	if (ret == `browser){
	 if (Support::browser==nil) Popup::Error(_("Couldn't find any installed browser."));
	  else{
          string url="'http://www.novell.com/center/eservice'";
	  if (0 == SCR::Execute (.target.bash, "env|grep LOGNAME|cut -d'=' -f2- | grep root"))
	  {
	    if (Popup::ContinueCancel (sformat (_("YaST will run a web browser as superuser. Consider
running it as a non-provileged user and entering URL
%1.
Start web browser?"), url)))
	    {
	      y2milestone("Executing browser %1 with URL %2", Support::browser, url);
	      SCR::Execute(.target.bash, sformat("%1 %2", Support::browser, url));
	    }
	  }
	  else
	  {
	      y2milestone("Executing browser %1 with URL %2", Support::browser, url);
	      SCR::Execute(.target.bash, sformat("su $(env|grep LOGNAME|cut -d'=' -f2-) -c \"%1 %2\"", Support::browser, url));
	  }
	  }
	 }
       }
 if (ret==`cancel) ret=`abort;
 return ret;
}

any UploadDialog(boolean data_prepared){
    string caption = _("Supportconfig Upload Dialog");
	string url=deletechars(Support::options["VAR_OPTION_UPLOAD_TARGET"]:"", "'");
	y2milestone("URL value from /etc/supportconfig.conf : %1", url);
    y2milestone("%1", Support::log_files);
    string home=((map<string, any>)SCR::Execute(.target.bash_output, "echo ~|tr -d '\n'"))["stdout"]:"";
    /* Support configure1 dialog contents */
    term load_save = nil;
    if (data_prepared)
     load_save=`Left(
		`CheckBoxFrame(`id(`save), _("Save as"), true,
		  `HBox(
                   `InputField(`id(`save_dir), _("Directory to Save"), home),
                   `VBox(
			`Label(""),
			`PushButton(`id(`browse), Label::BrowseButton())
			)
		       )
		      )
		    );
	else load_save=`Left(
	   `HBox(
		`InputField(`id(`tarball_file), _("Package with log files"), Support::log_files["tarball"]:""),
		`VBox(`Label(""), `PushButton(`id(`browse), Label::BrowseButton())))
        );

    term contents = `Frame("",
	`VBox(
	 load_save,
	    `CheckBoxFrame(`id(`upload), _("Upload log files tarball into URL"), true,
	      `Left( `InputField(`id(`url), _("Upload Target"), url) )
	   )
	 )
	);
    string help= (data_prepared) ? HELPS["upload_save"]:"" : HELPS["upload_select"]:"";
    Wizard::SetContentsButtons(caption, contents, help,
            Label::BackButton(), Label::NextButton());

    any ret = nil;
    while(true) {

        ret = UI::UserInput();
        if (ret == `abort || ret == `back)
                break;
        if (ret == `next){
	 if (!data_prepared){
	  string unpack=sformat("cd %1 && tar xvf %2", Support::log_files["tmp_dir"]:"", Support::log_files["tarball"]:"");
	  y2milestone("unpack %1", SCR::Execute(.target.bash_output, unpack));
//	  break;
	 }
	y2milestone("data_prepared %1", data_prepared);
	y2milestone("Support::log_files %1", Support::log_files);
          string command = sformat("supportconfig %1 -f %2", Support::GetParameterList(), Support::log_files["tmp_dir"]:"");
          if ((boolean)UI::QueryWidget(`upload, `Value)){
	   string url = (string)UI::QueryWidget(`url, `Value);
           if (size(url)>0) //{
/*
	    if (issubstring(url, "https://secure-www.novell.com/upload?appname=supportconfig&file"))
			command = sformat("%1 -u -U %2", command, sformat("https://secure-www.novell.com/upload?appname=supportconfig&file=%1", Support::log_files["tarball"]:""));
		else 
*/
		command = sformat("%1 -u -U '%2'", command, url);
//	   }
          }
	  if (Support::WhoAmI() != 0) {
	    if (! Support::AskForRootPwd())
	      return `back;
	    integer id = Support::WhoAmI();
	    SCR::Write (.target.string, Support::pwd_file, Support::root_pw + "\n");
	    command = sformat("cat %2 | su -c '%1'", command, Support::pwd_file);
          }
          y2milestone("executing %1", command);
          map<string, any> output = (map<string, any>)SCR::Execute(.target.bash_output, command);
          y2milestone("output %1", output);
	  if (Support::WhoAmI() != 0)
	    SCR::Write (.target.string, Support::pwd_file, "");
          if (output["exit"]:-1 != 0) Report::Error (sformat("%1 : %2",_("Cannot write settings"), output));
		else{
			command = sformat("find \"%1\" -type f -name \"%2*\"|grep -v \".md5$\" | tr -d '\n'", Support::log_files["tmp_dir"]:"", Support::log_files["log_dir"]:"");
			y2internal("command %1", command);
			output=(map<string, any>)SCR::Execute(.target.bash_output, command);
			if (output["exit"]:-1 != 0) Report::Error (sformat("%1 : %2", _("Cannot write settings."), output));
			 else{
				if(size(output["stdout"]:"")>0){
					 Support::log_files["tarball"]=output["stdout"]:"";
					 y2internal("input tarball : %1", Support::log_files["tarball"]:"");
					 if (data_prepared && (boolean)UI::QueryWidget(`save, `Value)) {
						string tarball = Support::log_files["tarball"]:"";
						string output_dir = (string)UI::QueryWidget(`save_dir, `Value);
						string command = sformat("cp %1* '%2'", tarball, output_dir);
						y2milestone("execute %1 : %2", command, SCR::Execute(.target.bash_output, command));
					 }
					}
					else y2error("Empty filename : %1", output);
			     }
		    }
         break;
	}
        if (ret == `browse){
	 if (data_prepared){
	  string startdir = (string)UI::QueryWidget(`save_dir, `Value);
	  y2milestone("startdir %1", startdir);
          string save_dir = UI::AskForExistingDirectory(startdir, _("Choose Directory Where to Save Tarball") );
          if (save_dir != nil && size(save_dir)>0){
           UI::ChangeWidget(`save_dir, `Value, save_dir);
//           Support::log_files["tmp_dir"]=save_dir;
          }
          else
           y2error("Empty or invalid logs tarball path");
         }else{
          string tarball_file = UI::AskForExistingFile("/", "*.tgz *.tbz", _("Choose Log Files Tarball File") );
          if (tarball_file != nil && size(tarball_file)>0){
           UI::ChangeWidget(`tarball_file, `Value, tarball_file);
           Support::log_files["tarball"]=tarball_file;
          }
          else
           y2error("Empty or invalid logs tarball path");
	}
         continue;
        }
    }
 return ret;
}

/**
 * Command line parameters dialog
 * @return dialog result
 */
any ParametersDialog () {

    /* Command line parameters dialog caption */
    string caption = _("Supportconfig Parameters Configuration");

    list<term> items = [
//	  `item(`id(`use_defaults), _("Use Defaults (ignore /etc/supportconfig.conf)"), Support::use_defaults),
	  `item(`id(`full_listening), _("Create a full file listing from '/'"), Support::full_listening),
	  `item(`id(`exclude_disk_scan), _("Exclude detailed disk info and scans"), Support::exclude_disk_scan),
	  `item(`id(`search_for_edir), _("Search root filesystem for eDirectory instances"), Support::search_for_edir),
//	  `item(`id(`full_logging), _("Activates all support functions"), Support::full_logging),
//	  `item(`id(`minimal_logs), _("Only gather a minimum amount of info"), Support::minimal_logs),
	  `item(`id(`include_slp), _("Include full SLP service lists"), Support::include_slp),
	  `item(`id(`rpm_check), _("Performs an rpm -V for each installed rpm"), Support::rpm_check)
	];
    /* Support configure1 dialog contents */
    term contents = `VBox(
       `Left(
	`RadioButtonGroup(`id(`rb),
	 `VBox(
	  `Left( `RadioButton(`id(`use_defaults), `opt(`notify), _("Use Defaults (ignore /etc/supportconfig.conf)")) ),
	  `Left( `RadioButton(`id(`full_logging), `opt(`notify), _("Activates all support functions")) ),
	  `Left( `RadioButton(`id(`minimal_logs), `opt(`notify), _("Only gather a minimum amount of info")) ),
	  `Left( `VBox(
		`RadioButton(`id(`custom), `opt(`notify), _("Use Custom (Expert) Settings")),
		`PushButton(`id(`expert), _("Expert Settings"))
		)
		)
	 )
	)
       ),
	`MultiSelectionBox(`id(`options), _("Options"), items)
/*
	`Left(
	 `HBox(
	  `InputField(`id(`log_dir), _("Log directory"), Support::log_files["tmp_dir"]:""),
	  `VBox(`Label(""), `PushButton(`id(`browse), Label::BrowseButton()))
	 )),
	`MultiSelectionBox(`id(`options), _("Options"), items),
*/
	);

    Wizard::SetContentsButtons(caption, contents, HELPS["support_params"]:"",
	    Label::BackButton(), Label::NextButton());

if (Support::use_defaults) UI::ChangeWidget(`rb, `CurrentButton, `use_defaults);
 else if (Support::full_logging) UI::ChangeWidget(`rb, `CurrentButton, `full_logging);
  else if (Support::minimal_logs) UI::ChangeWidget(`rb, `CurrentButton, `minimal_logs);
   else UI::ChangeWidget(`rb, `CurrentButton, `custom);

//    Wizard::DisableBackButton();

    any ret = nil;
    while(true) {
	UI::ChangeWidget(`expert, `Enabled, (UI::QueryWidget(`rb, `CurrentButton)==`custom));

	ret = UI::UserInput();

	if (ret==`use_defaults || ret ==`full_logging || ret ==`minimal_logs || ret ==`custom) continue;

	/* abort? */
	if(ret == `abort || ret == `cancel) {
	    if(ReallyAbort()) break;
	    else continue;
	}
	else if (ret == `back) break;
        else if(ret == `next || ret == `expert) {
	 list selected=(list)UI::QueryWidget(`options, `SelectedItems);
	 Support::use_defaults=contains(selected, `use_defaults);
	 Support::full_listening=contains(selected, `full_listening);
	 Support::exclude_disk_scan=contains(selected, `exclude_disk_scan);
	 Support::search_for_edir=contains(selected, `search_for_edir);
	 Support::full_logging=contains(selected, `full_logging);
	 Support::minimal_logs=contains(selected, `minimal_logs);
	 Support::include_slp=contains(selected, `include_slp);
	 Support::rpm_check=contains(selected, `rpm_check);

	 switch((symbol)UI::QueryWidget(`rb, `CurrentButton)){
	  case `use_defaults:
			Support::use_defaults=true;
			break;
	  case `full_logging:
			Support::full_logging=true;
			break;
	  case `minimal_logs:
			Support::minimal_logs=true;
			break;
	  default:
		y2milestone("Custom settings");
		break;
	 }
            break;
        }
         else {
            y2error("unexpected retcode: %1", ret);
            continue;
         }
    }

    return ret;
}

/**
 * Overview dialog
 * @return dialog result
 */
symbol ExpertDialog() {

    /* Support overview dialog caption */
    string caption = _("Supportconfig Expert Configuration");

    list overview = Support::Overview();

	list<term> bool_items = [];
//	list<term> table_items = [];
    foreach(string key, (list<string>)Map::Keys(Support::options), {
     if (!issubstring(key, "VAR_OPTION"))
      {
       string value = support_descr[key]:"";
       bool_items = add(bool_items, `item(`id(key), (size(value)>0) ? sformat("%1 - %2", key, value) : key, Support::options[key]:""=="1"));
      }
//	else
//         table_items = add(table_items, `item(`id(key), key, Support::options[key]:""));
   });

    /* FIXME table header */
    term contents =
       `HBox(
	`MultiSelectionBox(`id(`opt_msb), _("Default Options"), bool_items) //,
/*
	`VBox(
	 `Table(`id(`var_table), `header(_("Name"), _("Value")), table_items),
		`PushButton(`id(`edit), Label::EditButton())
	 )
*/
       );

    Wizard::SetContentsButtons(caption, contents, HELPS["expert_params"]:"",
	    Label::BackButton(), Label::NextButton());

    any ret = nil;
    while(true) {

	ret = UI::UserInput();

	/* abort? */
	if(ret == `abort || ret == `cancel) {
	    if(ReallyAbort()) break;
	    else continue;
	}
        /* edit */
/*
        else if(ret == `edit) {
	    any id = UI::QueryWidget(`var_table, `CurrentItem);
	    term row = (term)UI::QueryWidget(`var_table, `Item(id));
	    UI::OpenDialog(`VBox(
		 `InputField(`id(`changed_value), row[1]:"", row[2]:""),
		  `HBox(
		   `PushButton(`id(`cancel), Label::CancelButton()),
		   `PushButton(`id(`ok), Label::OKButton())
		  )
		));
	    any ret=UI::UserInput();
	    if (ret==`ok){
		string new_value = (string)UI::QueryWidget(`id(`changed_value), `Value);
		if (new_value != row[2]:"") {
			UI::CloseDialog();
			UI::ChangeWidget(`id(`var_table), `Cell(`id(id), 1), new_value);
			continue;
		}
	    }
	    UI::CloseDialog();
            continue;
        }
*/
        else if( ret == `next ) {
	 y2milestone("store configuration for /etc/supportconfig.conf");
	 list selected_items = (list)UI::QueryWidget(`id(`opt_msb), `SelectedItems);
	 foreach(string key, (list<string>)Map::Keys(Support::options), {
	  string val = Support::options[key]:"";
	  if (!issubstring(key, "VAR_OPTION")){
	    boolean bool_val=(contains(selected_items, key));
	    if ((val=="1")!=bool_val){
	     y2internal("value changed %1=%2, new value %3", key, val, bool_val ? "1" : "0");
	     Support::options[key]=bool_val ? "1" : "0";
	    }
	  }
/*
	 else{
	   string new_val = (string)UI::QueryWidget(`var_table, `Cell(`id(key), 1));
	   if(new_val!=val){
	     y2internal("value changed %1=%2, new value %3", key, val, new_val);
	     Support::options[key]=new_val;
	   }
	  }
*/
	 });
            break;
        }
	else if ( ret == `back ) break;
		else {
		 y2error("unexpected retcode: %1", ret);
		 continue;
		}
    }
y2internal("%1", Support::options);
    return (symbol)ret;
}

/**
 * Configure2 dialog
 * @return dialog result
 */
any ContactDialog () {

    /* Support configure2 dialog caption */
    string caption = _("Supportconfig Contact Configuration");

    /* Support configure2 dialog contents */
    term contents = `VBox(
	`Frame (_("Contact Information"), `VBox(
	  `Left(`InputField(`id(`company), _("Company"), Support::options["VAR_OPTION_CONTACT_COMPANY"]:"")),
	  `Left(`InputField(`id(`email), _("Email Address"), Support::options["VAR_OPTION_CONTACT_EMAIL"]:"")),
	  `Left(`InputField(`id(`name), _("Name"), Support::options["VAR_OPTION_CONTACT_NAME"]:"")),
	  `Left(`InputField(`id(`phone), _("Phone Number"), Support::options["VAR_OPTION_CONTACT_PHONE"]:"")),
	  `Left(`InputField(`id(`storeid), _("Store ID"), Support::options["VAR_OPTION_CONTACT_STOREID"]:"")),
	  `Left(`InputField(`id(`terminalid), _("Terminal ID"), Support::options["VAR_OPTION_CONTACT_TERMINALID"]:""))
	 )),
	`Frame (_("Upload Information"), `VBox(
	  `Left(`InputField(`id(`target), _("Upload Target"), deletechars(Support::options["VAR_OPTION_UPLOAD_TARGET"]:"", "'"))),
	  `Left(`InputField(`id(`novell_number), _("Novell 11 digit service request number"), Support::novell_number))
	)));

    Wizard::SetContentsButtons(caption, contents, HELPS["contact"]:"",
	    Label::BackButton(), Label::NextButton());
    UI::ChangeWidget(`novell_number, `ValidChars, "0123456789");

    any ret = nil;
    while(true) {

	ret = UI::UserInput();

	/* abort? */
	if(ret == `abort || ret == `cancel) {
	    if(ReallyAbort()) break;
	    else continue;
	}
        else if(ret == `next || ret == `back) {
	  Support::options["VAR_OPTION_CONTACT_COMPANY"]=(string)UI::QueryWidget(`company, `Value);
	  Support::options["VAR_OPTION_CONTACT_EMAIL"]=(string)UI::QueryWidget(`email, `Value);
	  Support::options["VAR_OPTION_CONTACT_NAME"]=(string)UI::QueryWidget(`name, `Value);
	  Support::options["VAR_OPTION_CONTACT_PHONE"]=(string)UI::QueryWidget(`phone, `Value);
	  Support::options["VAR_OPTION_CONTACT_STOREID"]=(string)UI::QueryWidget(`storeid, `Value);
	  Support::options["VAR_OPTION_CONTACT_TERMINALID"]=(string)UI::QueryWidget(`terminalid, `Value);
	  Support::options["VAR_OPTION_UPLOAD_TARGET"]=sformat("'%1'", (string)UI::QueryWidget(`target, `Value));
	  Support::novell_number=(string)UI::QueryWidget(`novell_number, `Value);
	  if (size(Support::novell_number)>0){
		if (size(Support::novell_number)!=11){
		 Popup::Error(_("The SR number must be 11 digits"));
		 ret=nil;
		 continue;
		}
	  }
	  Support::WriteConfig();
          break;
        }
        else {
            y2error("unexpected retcode: %1", ret);
            continue;
        }
    }

    return ret;
}


symbol GenerateDialog () {
    string caption = _("Collecting Data");
    term contents = `VBox (
        `LogView (`id (`log), _("Progress"), 1000, 1000)
    );
    Wizard::SetContentsButtons(caption, contents, HELPS["collecting"]:"",
            Label::BackButton(), Label::NextButton());
    string uuid_param = (issubstring(Support::options["VAR_OPTION_UPLOAD_TARGET"]:"", "novell.com")) ? "-q" : "";
    string cmd = sformat("supportconfig %1 %2 -t %3", Support::GetParameterList(), uuid_param, Support::log_files["tmp_dir"]:"");
    if (Support::WhoAmI() != 0) {
	if (! Support::AskForRootPwd())
	    return `back;
	integer id = Support::WhoAmI();
	SCR::Write (.target.string, Support::pwd_file, Support::root_pw + "\n");
	cmd = sformat("cat %4 | su -c '%1 && chown -R %3 %2'", cmd, Support::log_files["tmp_dir"]:"", id, Support::pwd_file);
    }
    symbol ret = nil;
    integer pid = (integer) SCR::Execute(.process.start_shell, cmd);
    string unfinished_line = "";
    Wizard::DisableNextButton ();
    while (true) {
        sleep (100);
        if (SCR::Read (.process.running, pid) == true) {
            string new_text = (string)SCR::Read (.process.read, pid);
            if (new_text != nil)
                UI::ChangeWidget (`id (`log), `LastLine, new_text);
        } else {
	    Wizard::EnableNextButton ();
	    break;
        }
        ret = (symbol)UI::PollInput();
        if (ret == `back || ret == `abort )
        {
            SCR::Execute (.process.kill, pid);
            break;
        }
    }
    if (Support::WhoAmI() != 0)
	SCR::Write (.target.string, Support::pwd_file, "");
    while(ret!=`back && ret!=`abort && ret!=`next){
     ret = (symbol)UI::UserInput();
    }
    return ret;
}



symbol FilesDialog() {
    string caption = _("Collected Data Review");
    // FIXME use list of generated files, as well as directory prefix
	 map output = (map)SCR::Execute(.target.bash_output, sformat("ls -t %1|grep nts|head -n1|tr -d '\n'", Support::log_files["tmp_dir"]:""));
	 y2internal("output %1", output);
	 if (output["exit"]:-1!=0){
	   Popup::Error(output["stderr"]:"");
	   return `back;
	 }
	 Support::log_files["log_dir"]=output["stdout"]:"";
	 string full_log_path=sformat("%1/%2/", Support::log_files["tmp_dir"]:"", Support::log_files["log_dir"]:"");
	 output = (map)SCR::Execute(.target.bash_output, sformat("ls %1", full_log_path));
         if (output["exit"]:-1!=0){
           Popup::Error(output["stderr"]:"");
           return `back;
         }
    list<string> files = filter(string s, splitstring(output["stdout"]:"", "\n"), {return (size(s)>0);});
    term contents = `VBox (
	`HBox (
	    `HStretch(),
	    `ReplacePoint (`id (`filelist_rp),
		`ComboBox (`id (`filelist), `opt (`notify, `hstretch), _("File Name"), files)),
	    `VBox (
		`Label(""),
		`PushButton (`id (`remove), `opt (`hstretch), _("Remove from Data"))
	    ),
	    `HStretch()
	),
	`ReplacePoint (`id (`file_rp),
	    `RichText (`id (`file), `opt (`plainText), "")
//	`MultiLineEdit (`id (`file), `opt (`read_only), _("File Contents"))
	)
    );
    Wizard::SetContentsButtons(caption, contents, HELPS["review"]:"",
            Label::BackButton(), Label::NextButton());
    symbol ret = `filelist;
    while (true) {
	if (ret == `filelist)
	{
	    string file = full_log_path + (string)UI::QueryWidget (`id (`filelist), `Value);
	    string data = (string)SCR::Read (.target.string, file);
	    UI::ReplaceWidget (`id (`file_rp),
		`RichText (`id (`file), `opt (`plainText), data));
//	    UI::ChangeWidget (`id (`file), `Value, data);
	}
	ret = (symbol)UI::UserInput();
	if (ret == `next ){
/*
          string command = sformat("supportconfig %1 -f %2", Support::GetParameterList(), Support::log_files["tmp_dir"]:"");
          y2internal("executing %1", command);
          map<string, any> output = (map<string, any>)SCR::Execute(.target.bash_output, command);
          y2milestone("output %1", output);
          if (output["exit"]:-1 != 0) Report::Error (sformat("%1 : %2",_("Cannot write settings"), output));
		else{
			command = sformat("find \"%1\" -type f -name \"%2*\"|tr -d '\n'", Support::log_files["tmp_dir"]:"", Support::log_files["log_dir"]:"");
			y2internal("command %1", command);
			output=(map<string, any>)SCR::Execute(.target.bash_output, command);
			if (output["exit"]:-1 != 0) Report::Error (sformat("%1 : %2", _("Cannot write settings."), output));
			 else{
				if(size(output["stdout"]:"")>0) Support::log_files["tarball"]=output["stdout"]:"";
					else y2error("Empty filename : %1", output);
			     }
		    }
*/
	  break;
	}
	if (ret == `abort || ret == `back)
	    break;
	if (ret == `remove)
	{
	    string file = (string)UI::QueryWidget (`id (`filelist), `Value);
	    files = filter (string f, files, { return f != file; });
	    UI::ReplaceWidget (`id (`filelist_rp),
		`ComboBox (`id (`filelist), `opt (`notify, `hstretch), _("File Name"), files));
	    ret = `filelist;
// FIXME uncomment, following line not tested
	y2internal("removing %1%2", full_log_path, file);
        SCR::Execute (.target.bash, sformat("/bin/rm %1%2", full_log_path, file));
	}
    }
    return ret;
}
/* EOF */
}
